apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.example.androidthings.photobooth"
        minSdkVersion 26
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

// Download model zip file into ../../assets directory
// unzip it to demo project's own ./assets directory
import de.undercouch.gradle.tasks.download.Download
task downloadFile(type: Download) {
    src 'https://storage.googleapis.com/download.tensorflow.org/models/stylize_v1.zip'
    dest projectDir.toString() + '/../../assets/stylize.zip'
}

task unzip(type: Copy) {
    from zipTree(projectDir.toString() + '/../../assets/stylize.zip')
    into file(projectDir.toString() + '/assets')
}
unzip.dependsOn downloadFile

project.afterEvaluate {
    if (!(new File(projectDir.toString() + '/assets')).exists()) {
        preBuild.dependsOn unzip
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation 'com.android.support:support-v13:25.3.1'
    implementation 'org.tensorflow:tensorflow-android:1.4.0'

    compileOnly 'com.google.android.things:androidthings:0.6-devpreview'
    implementation 'com.google.android.things.contrib:driver-button:0.2'

    implementation 'com.google.firebase:firebase-auth:10.2.4'
    implementation 'com.google.firebase:firebase-storage:10.2.4'
    implementation 'com.google.firebase:firebase-database:10.2.4'
    implementation 'com.google.firebase:firebase-messaging:10.2.4'

    implementation 'com.google.zxing:core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
